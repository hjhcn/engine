# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

group("editing") {
  public_deps = [
    ":interfaces",
  ]

  if (is_android || is_ios) {
    public_deps += [ ":editing_lib" ]
  }
}

mojom("interfaces") {
  sources = [
    "editing.mojom",
  ]
}

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  android_library("editing_lib") {
    java_files = [
      "src/org/domokit/editing/KeyboardImpl.java",
      "src/org/domokit/editing/KeyboardViewState.java",
      "src/org/domokit/editing/InputConnectionAdaptor.java",
    ]

    deps = [
      "//base:base_java",
      "//mojo/public/java:bindings",
      "//mojo/public/java:system",
      ":interfaces_java",
    ]
  }
}

if (is_ios) {
  source_set("editing_lib") {
    sources = [
      "ios/keyboard_impl.h",
      "ios/keyboard_impl.mm",
    ]
    deps = [
      "//base:base",
      "//mojo/public/cpp/bindings:utility",
      "//mojo/public/cpp/application",
      ":interfaces",
    ]
  }
}
