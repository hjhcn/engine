# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/shell/config.gni")
import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

generate_jni("jni_headers") {
  visibility = [ ":*" ]

  sources = [
    "io/flutter/view/FlutterMain.java",
    "io/flutter/view/FlutterView.java",
    "io/flutter/view/VsyncWaiter.java",
  ]
  jni_package = "shell"
}

shared_library("sky_shell") {
  visibility = [ ":*" ]

  sources = [
    "android_context_gl.cc",
    "android_context_gl.h",
    "android_environment_gl.cc",
    "android_environment_gl.h",
    "android_native_window.cc",
    "android_native_window.h",
    "android_surface.cc",
    "android_surface.h",
    "android_surface_gl.cc",
    "android_surface_gl.h",
    "flutter_main.cc",
    "flutter_main.h",
    "library_loader.cc",
    "platform_view_android.cc",
    "platform_view_android.h",
    "vsync_waiter_android.cc",
    "vsync_waiter_android.h",
  ]

  deps = [
    ":jni_headers",
    "//base",
    "//dart/runtime:libdart",
    "//flutter/common",
    "//flutter/flow",
    "//flutter/lib/jni",
    "//flutter/lib/ui",
    "//flutter/runtime",
    "//flutter/shell/common",
    "//flutter/shell/gpu",
    "//lib/ftl",
    "//third_party/skia",
  ]

  defines = []

  if (shell_enable_vulkan) {
    sources += [
      "android_surface_vulkan.cc",
      "android_surface_vulkan.h",
    ]

    deps += [ "//flutter/vulkan" ]

    defines += [ "SHELL_ENABLE_VULKAN" ]
  }

  ldflags = [
    "-landroid",
    "-lEGL",
    "-lGLESv2",
  ]
}

android_library("java") {
  visibility = [ ":*" ]

  java_files = [
    "io/flutter/app/FlutterActivity.java",
    "io/flutter/app/FlutterApplication.java",
    "io/flutter/plugin/common/ActivityLifecycleListener.java",
    "io/flutter/plugin/common/BinaryMessageCodec.java",
    "io/flutter/plugin/common/FlutterMessageChannel.java",
    "io/flutter/plugin/common/FlutterMethodChannel.java",
    "io/flutter/plugin/common/JSONMessageCodec.java",
    "io/flutter/plugin/common/JSONMessageListener.java",
    "io/flutter/plugin/common/JSONMethodCodec.java",
    "io/flutter/plugin/common/MessageCodec.java",
    "io/flutter/plugin/common/MethodCodec.java",
    "io/flutter/plugin/common/MethodCall.java",
    "io/flutter/plugin/common/StandardMessageCodec.java",
    "io/flutter/plugin/common/StandardMethodCodec.java",
    "io/flutter/plugin/common/StringMessageCodec.java",
    "io/flutter/plugin/editing/InputConnectionAdaptor.java",
    "io/flutter/plugin/editing/TextInputPlugin.java",
    "io/flutter/plugin/platform/PlatformPlugin.java",
    "io/flutter/view/AccessibilityBridge.java",
    "io/flutter/view/FlutterMain.java",
    "io/flutter/view/FlutterView.java",
    "io/flutter/view/ResourceCleaner.java",
    "io/flutter/view/ResourceExtractor.java",
    "io/flutter/view/ResourcePaths.java",
    "io/flutter/view/VsyncWaiter.java",
  ]

  deps = [
    "//base:base_java",
  ]
}

copy_ex("assets") {
  visibility = [ ":*" ]

  clear_dir = true
  dest = "$root_build_dir/sky_shell/assets"
  sources = [
    "$root_build_dir/icudtl.dat",
  ]
  deps = [
    "//third_party/icu:icudata",
  ]
}

android_apk("android") {
  apk_name = "SkyShell"
  android_manifest = "AndroidManifest.xml"

  native_libs = [ "libsky_shell.so" ]
  asset_location = "$root_build_dir/sky_shell/assets"

  extensions_to_not_compress = ".flx"

  flutter_dist_jar = "$root_build_dir/flutter.jar"

  deps = [
    ":assets",
    ":java",
    ":sky_shell",
    "//base:base_java",
  ]
}
