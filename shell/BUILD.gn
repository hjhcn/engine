# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_android)

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

group("shell") {
  deps = [
    ":sky_shell_apk",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "apk/src/org/domokit/sky/shell/SkyMain.java",
    "apk/src/org/domokit/sky/shell/SkyView.java",
  ]
  jni_package = "sky/shell"
}

shared_library("sky_shell") {
  sources = [
    "gpu/ganesh_context.cc",
    "gpu/ganesh_context.h",
    "gpu/ganesh_surface.cc",
    "gpu/ganesh_surface.h",
    "gpu/rasterizer.cc",
    "gpu/rasterizer.h",
    "ui/engine.cc",
    "ui/engine.h",
    "ui/platform_impl.cc",
    "ui/platform_impl.h",
    "library_loader.cc",
    "shell.cc",
    "shell.h",
    "sky_main.cc",
    "sky_main.h",
    "sky_view.cc",
    "sky_view.h",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//build/config/sanitizers:deps",
    "//mojo/edk/system",
    "//skia",
    "//sky/engine",
    "//ui/gfx/geometry",
    "//ui/gl",
    ":jni_headers",
  ]
}

android_library("java") {
  java_files = [
    "apk/src/org/domokit/sky/shell/SkyMain.java",
    "apk/src/org/domokit/sky/shell/SkyShellActivity.java",
    "apk/src/org/domokit/sky/shell/SkyShellApplication.java",
    "apk/src/org/domokit/sky/shell/SkyView.java",
  ]

  deps = [
    "//base:base_java",
  ]
}

android_resources("resources") {
  resource_dirs = [ "apk/res" ]
  custom_package = "org.domokit.sky.shell"
}

android_apk("sky_shell_apk") {
  apk_name = "SkyShell"

  android_manifest = "apk/AndroidManifest.xml"
  native_libs = [ "libsky_shell.so" ]

  asset_location = "apk/res"

  deps = [
    ":sky_shell",
    ":java",
    ":resources",
    "//base:base_java",
  ]
}
