# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//sky/build/skyx.gni")

template("sky_apk") {
  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = invoker.android_manifest

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$root_build_dir/sky_shell/assets"

    deps = [
             "//base:base_java",
             "//sky/shell:assets",
             "//sky/shell:java",
             "//sky/shell:sky_shell",
           ] + invoker.deps
  }
}

template("sky_app") {
  skyx("app") {
    main_dart = invoker.main_dart

    if (defined(invoker.manifest)) {
      manifest = invoker.manifest
    }

    if (defined(invoker.sources)) {
      sources = invoker.sources
    }
  }

  copy_ex("assets") {
    clear_dir = true
    dest = "$target_gen_dir/assets"

    sources = [
      "$root_build_dir/icudtl.dat",
    ]
    deps = [
      "//third_party/icu",
    ]

    if (enable_skyx) {
      sources += [ "$target_gen_dir/app.skyx" ]
      deps += [ ":app" ]
    }
  }

  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = "apk/AndroidManifest.xml"

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$target_gen_dir/assets"

    deps = [
      "//base:base_java",
      "//sky/shell:assets",
      "//sky/shell:java",
      "//sky/shell:sky_shell",
      ":assets",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}
