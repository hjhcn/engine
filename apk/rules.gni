# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

package_root = "$root_gen_dir/dart-pkg/packages"

template("sky_apk") {
  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = invoker.android_manifest

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$root_build_dir/sky_shell/assets"

    deps = [
             "//base:base_java",
             "//sky/shell:assets",
             "//sky/shell:java",
             "//sky/shell:sky_shell",
           ] + invoker.deps
  }
}

template("sky_app") {
  # Note: org.chromium.base.ResourceExtractor knows about 'snapshot_blob.bin'.
  snapshot = "$target_gen_dir/snapshot_blob.bin"

  action("gen_snapshot") {
    main_dart = invoker.main_dart

    inputs = [
      main_dart,
    ]
    outputs = [
      snapshot,
    ]

    sky_packager_dir =
        get_label_info("//sky/tools/packager($host_toolchain)", "root_out_dir")

    script = "//sky/tools/sky_packager.py"

    src_dir = "//"
    cwd = rebase_path(src_dir, root_build_dir)

    args = [
      rebase_path("$sky_packager_dir/sky_packager", src_dir),
      rebase_path(main_dart, src_dir),
      "--package-root",
      rebase_path(package_root, src_dir),
      "--snapshot",
      rebase_path(snapshot, src_dir),
      "-C",
      cwd,
    ]

    deps = [
      "//sky/tools/packager($host_toolchain)",
      "//sky/sdk",
    ]
  }

  if (defined(invoker.sky_yaml)) {
    app_bundle = "$target_gen_dir/${target_name}.skyx"

    action("gen_bundle") {
      asset_base = "$package_root/sky/assets/material-design-icons"
      sky_yaml = invoker.sky_yaml

      sources = [
        sky_yaml,
        snapshot,
      ]

      outputs = [
        app_bundle,
      ]

      script = "//sky/tools/skyx.py"

      args = [
        rebase_path(sky_yaml, root_build_dir),
        "--asset-base",
        rebase_path(asset_base, root_build_dir),
        "--snapshot",
        rebase_path(snapshot, root_build_dir),
        "-o",
        rebase_path(app_bundle, root_build_dir),
      ]

      deps = [
        ":gen_snapshot",
      ]
    }
  }

  copy_ex("assets") {
    clear_dir = true
    dest = "$target_gen_dir/assets"

    sources = [
      "$root_build_dir/icudtl.dat",
      "$target_gen_dir/snapshot_blob.bin",
    ]

    deps = [
      "//third_party/icu",
    ]
  }

  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = "apk/AndroidManifest.xml"

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$target_gen_dir/assets"

    deps = [
      "//base:base_java",
      "//sky/shell:assets",
      "//sky/shell:java",
      "//sky/shell:sky_shell",
      ":assets",
      ":gen_snapshot",
    ]

    if (defined(invoker.sky_yaml)) {
      deps += [ ":gen_bundle" ]
    }

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}
