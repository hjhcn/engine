# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

declare_args() {
  # Controls whether we build app.skyx bundles in the sky_app template below.
  # An app.skyx bundle contains all the code an resources needed to run a Sky
  # app offline. Unfortunately, we don't yet have all the Dart packages we need
  # to create skyx packages installed on the build bots, so this functionality
  # is currently disabled. This flag allows for local testing of this feature.
  enable_skyx = false
}

package_root = "$root_gen_dir/dart-pkg/packages"

template("sky_apk") {
  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = invoker.android_manifest

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$root_build_dir/sky_shell/assets"

    deps = [
             "//base:base_java",
             "//sky/shell:assets",
             "//sky/shell:java",
             "//sky/shell:sky_shell",
           ] + invoker.deps
  }
}

template("sky_app") {
  # Note: org.domokit.sky.shell.SkyApplication knows about 'snapshot_blob.bin'.
  snapshot = "$target_gen_dir/snapshot_blob.bin"

  action("gen_snapshot") {
    main_dart = invoker.main_dart

    inputs = [
      main_dart,
    ]
    outputs = [
      snapshot,
    ]

    sky_packager_dir =
        get_label_info("//sky/tools/packager($host_toolchain)", "root_out_dir")

    script = "//sky/tools/sky_packager.py"

    src_dir = "//"
    cwd = rebase_path(src_dir, root_build_dir)

    args = [
      rebase_path("$sky_packager_dir/sky_packager", src_dir),
      rebase_path(main_dart, src_dir),
      "--package-root",
      rebase_path(package_root, src_dir),
      "--snapshot",
      rebase_path(snapshot, src_dir),
      "-C",
      cwd,
    ]

    deps = [
      "//sky/tools/packager($host_toolchain)",
      "//sky/sdk",
    ]
  }

  app_bundle = "$target_gen_dir/app.skyx"

  action("gen_bundle") {
    asset_base = "$package_root/sky/assets/material-design-icons"
    sky_yaml = invoker.sky_yaml

    sources = [
      "//sky/tools/skyx/bin/skyx.dart",
      sky_yaml,
      snapshot,
    ]

    outputs = [
      app_bundle,
    ]

    script = "//sky/tools/skyx.py"
    args = [
      "--asset-base",
      rebase_path(asset_base, root_build_dir),
      "--snapshot",
      rebase_path(snapshot, root_build_dir),
      "-o",
      rebase_path(app_bundle, root_build_dir),
      rebase_path(sky_yaml, root_build_dir),
    ]

    deps = [
      ":gen_snapshot",
    ]
  }

  copy_ex("assets") {
    clear_dir = true
    dest = "$target_gen_dir/assets"

    sources = [
      "$root_build_dir/icudtl.dat",
    ]

    deps = [
      "//third_party/icu",
    ]

    if (enable_skyx) {
      sources += [ app_bundle ]
      deps += [ ":gen_bundle" ]
    }
  }

  android_apk(target_name) {
    apk_name = invoker.apk_name
    android_manifest = "apk/AndroidManifest.xml"

    native_libs = [ "libsky_shell.so" ]
    asset_location = "$target_gen_dir/assets"

    deps = [
      "//base:base_java",
      "//sky/shell:assets",
      "//sky/shell:java",
      "//sky/shell:sky_shell",
      ":assets",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}
