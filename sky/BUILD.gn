# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//sky/engine/bindings/bindings.gni")

declare_args() {
  enable_gcm = false
  enable_firebase = false
  enable_google_sign_in = false
}

group("sky") {
  testonly = true

  deps = [
    "//sky/engine/platform:platform_unittests($host_toolchain)",
    "//sky/engine/wtf:unittests($host_toolchain)",
    "//sky/packages",
    "//sky/shell",
  ]

  if (dart_host_toolchain == host_toolchain) {
    deps += [ "//sky/tools/sky_snapshot($dart_host_toolchain)" ]
  }

  if (is_linux || is_android) {
    deps += [ "//sky/shell/platform/mojo" ]
  }

  if (is_android) {
    deps += [ "//sky/services/activity" ]
  }

  # TODO(mpcomplete): This is temporary until we move GCM and Firebase
  # out of the engine repo.
  if (enable_firebase) {
    deps += [ "//third_party/firebase" ]
  }

  if (enable_gcm) {
    deps += [ "//third_party/gcm:gcm" ]
  }

  if (enable_google_sign_in) {
    deps += [ "//third_party/google_sign_in" ]
  }
}
