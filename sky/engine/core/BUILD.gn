# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/sky/engine/core/core.gni")

visibility = [ "//flutter/sky/engine/*", "//flutter/sky/shell/*" ]

source_set("libraries") {
  public_deps = [
    "//base",
    "//flutter/common",
    "//flutter/flow",
    "//flutter/glue",
    "//flutter/skia",
    "//flutter/sky/engine/wtf",
    "//lib/ftl",
    "//lib/tonic",
    "//lib/tonic/debugger",
    "//mojo/application",
    "//mojo/data_pipe_utils",
    "//mojo/public/c/system",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/cpp/utility",
    "//mojo/public/interfaces/application",
    "//mojo/public/platform/dart:mojo_internal_impl",
    "//third_party/iccjpeg",
    "//third_party/libpng",
    "//third_party/qcms",
    "//third_party/zlib",
  ]

  if (is_android) {
    public_deps += [
      "//flutter/lib/jni",
    ]
  }
}

source_set("prerequisites") {
  deps = [
    ":libraries",
    "//flutter/sky/engine/platform",
  ]

  forward_dependent_configs_from = [ ":libraries" ]

  direct_dependent_configs = [
    "//flutter/sky/engine:config",
    "//flutter/sky/engine:inside_blink",
    "//build/config/compiler:wexit_time_destructors",
  ]
}

static_library("core") {
  output_name = "sky_core"

  deps = [
    ":libraries",
    ":prerequisites",
    "//dart/runtime:libdart",
    "//dart/runtime/bin:embedded_dart_io",
    "//dart/runtime/vm:libdart_platform",
    "//flutter/assets",
    "//mojo/services/navigation/interfaces",
    "//flutter/sky/engine/platform",
    "//flutter/services/engine:interfaces",
    "//flutter/services/pointer:interfaces",
  ]

  if (flutter_runtime_mode != "release") {
    # Only include observatory assets in non-release modes.
    deps += [
      "//dart/runtime/observatory:embedded_observatory_archive",
    ]
  }

  sources = sky_core_files

  forward_dependent_configs_from = [ ":libraries" ]
}
