# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/sky/engine/core/core.gni")
import("//mojo/dart/embedder/embedder.gni")

visibility = [ "//flutter/sky/engine/*", "//flutter/sky/shell/*" ]

source_set("libraries") {
  public_deps = [
    "//base",
    "//flutter/common",
    "//flutter/flow",
    "//flutter/glue",
    "//flutter/lib/io",
    "//flutter/lib/ui",
    "//lib/ftl",
    "//lib/tonic",
    "//lib/tonic/debugger",
    "//mojo/application",
    "//mojo/data_pipe_utils",
    "//mojo/public/c/system",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/cpp/utility",
    "//mojo/public/interfaces/application",
    "//mojo/public/platform/dart:mojo_internal_impl",
    "//flutter/skia",
    "//flutter/sky/engine/wtf",
    "//third_party/iccjpeg",
    "//third_party/libpng",
    "//third_party/qcms",
    "//third_party/zlib",
  ]

  if (is_android) {
    public_deps += [
      "//flutter/lib/jni",
    ]
  }
}

source_set("prerequisites") {
  deps = [
    ":libraries",
    "//flutter/sky/engine/platform",
  ]

  forward_dependent_configs_from = [ ":libraries" ]

  direct_dependent_configs = [
    "//flutter/sky/engine:config",
    "//flutter/sky/engine:inside_blink",
    "//build/config/compiler:wexit_time_destructors",
  ]
}

vmservice_sources_gypi =
  exec_script("//build/gypi_to_gn.py",
      [ rebase_path("//dart/runtime/bin/vmservice/vmservice_sources.gypi") ],
      "scope",
      [ rebase_path("//dart/runtime/bin/vmservice/vmservice_sources.gypi") ])

dart_embedder_resources("generate_sky_embedder_service_isolate_resources_cc") {
  inputs = rebase_path(vmservice_sources_gypi.sources,
                       "",
                       "//dart/runtime/bin/vmservice")
  root_prefix = "//dart/runtime/bin/"
  output = "$target_gen_dir/sky_embedder_service_isolate_resources.cc"
  table_name = "sky_embedder_service_isolate"
}

static_library("core") {
  output_name = "sky_core"

  deps = [
    ":generate_sky_embedder_service_isolate_resources_cc",
    ":libraries",
    ":prerequisites",
    "//dart/runtime:libdart",
    "//dart/runtime/bin:embedded_dart_io",
    "//dart/runtime/vm:libdart_platform",
    "//flutter/assets",
    "//mojo/services/navigation/interfaces",
    "//flutter/sky/engine/bindings",
    "//flutter/sky/engine/platform",
    "//flutter/services/engine:interfaces",
    "//flutter/services/pointer:interfaces",
  ]

  if (!flutter_product_mode) {
    # Only include observatory assets in non-product modes.
    deps += [
      "//dart/runtime/observatory:embedded_observatory_archive",
    ]
  }

  sources = sky_core_files

  sources += [ "$target_gen_dir/sky_embedder_service_isolate_resources.cc" ]

  include_dirs = [
    # Needed for dart_mirrors_api.h in dart_controller.cc
    rebase_path("//dart/runtime"),
  ]

  forward_dependent_configs_from = [ ":libraries" ]

  # core and core_generated are really the same thing.
  allow_circular_includes_from = [
    "//flutter/sky/engine/bindings",
  ]
}
