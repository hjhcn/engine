# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/dart/packages/mojo/sdk_ext_sources.gni")
import("//sky/engine/bindings/bindings.gni")
import("//sky/engine/core/core.gni")

source_set("bindings") {
  sources = [
    "dart_mojo_internal.cc",
    "dart_mojo_internal.h",
    "dart_runtime_hooks.cc",
    "dart_runtime_hooks.h",
    "dart_ui.cc",
    "dart_ui.h",
    "exception_messages.cc",
    "exception_messages.h",
    "exception_state.cc",
    "exception_state.h",
    "exception_state_placeholder.cc",
    "exception_state_placeholder.h",
    "flutter_dart_state.cc",
    "flutter_dart_state.h",
    "mojo_services.cc",
    "mojo_services.h",
  ]

  if (is_android) {
    sources += [
      "jni/dart_jni.cc",
      "jni/dart_jni.h",
      "jni/jni_api.cc",
      "jni/jni_api.h",
      "jni/jni_array.cc",
      "jni/jni_array.h",
      "jni/jni_class.cc",
      "jni/jni_class.h",
      "jni/jni_object.cc",
      "jni/jni_object.h",
      "jni/jni_string.cc",
      "jni/jni_string.h",
    ]
  }

  defines = [ "DART_IO_SECURE_SOCKET_DISABLED" ]

  deps = [
    "//base",
    "//dart/runtime/bin:embedded_dart_io",
    "//dart/runtime:libdart",
    "//mojo/public/c/system",
    "//mojo/public/cpp/application",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//mojo/public/interfaces/application",
    "//mojo/public/platform/dart:mojo_internal_impl",
    "//mojo/services/asset_bundle/interfaces",
    "//services/asset_bundle:lib",
    "//sky/engine/core:prerequisites",
    "//sky/engine/platform:platform",
    "//sky/engine/tonic",
    "//sky/engine/wtf",
    "//sky/services/engine:interfaces",
  ]

  # On iOS (device), precompiled snapshots contain the instruction buffer.
  # Generation of the same requires all application specific script code to be
  # specified up front. In such cases, there can be no updater or generic
  # snapshotter.
  if (!is_ios || use_ios_simulator) {
    deps += [
      ":snapshot_cc",
    ]
  }

  include_dirs = [
    "..",
    "$root_build_dir",
    rebase_path("//dart/runtime"),
  ]
}

action("generate_snapshot_bin") {
  deps = [
    "//dart/runtime/bin:gen_snapshot($dart_host_toolchain)",
    ":generate_dart_ui",
    ":generate_dart_jni",
  ]
  inputs = [
    "//dart/runtime/tools/create_snapshot_bin.py",
    "//sky/engine/bindings/jni/jni.dart",
    "//sky/engine/bindings/jni/jni_raw.dart",
    "//sky/engine/bindings/jni/jni_helper.dart",
    "snapshot.dart",
  ] + rebase_path(dart_mojo_internal_sdk_sources,
                  "",
                  "//mojo/dart/packages/mojo")
  vm_isolate_snapshot = "$target_gen_dir/vm_isolate_snapshot.bin"
  isolate_snapshot = "$target_gen_dir/isolate_snapshot.bin"
  outputs = [
    vm_isolate_snapshot,
    isolate_snapshot,
  ]

  dart_mojo_internal_path =
      rebase_path("//mojo/dart/packages/mojo/sdk_ext/internal.dart")
  dart_ui_path = rebase_path("$bindings_output_dir/dart_ui.dart")
  dart_jni_path = rebase_path("//sky/engine/bindings/jni/jni.dart")

  gen_snapshot_dir =
      get_label_info("//dart/runtime/bin:gen_snapshot($host_toolchain)",
                     "root_out_dir")
  script = "//dart/runtime/tools/create_snapshot_bin.py"

  args = [
    "--executable",
    rebase_path("$gen_snapshot_dir/gen_snapshot"),
    "--package_root",
    rebase_path("$root_gen_dir"),
    "--script",
    rebase_path("snapshot.dart"),
    "--vm_output_bin",
    rebase_path(vm_isolate_snapshot, root_build_dir),
    "--output_bin",
    rebase_path(isolate_snapshot, root_build_dir),
    "--target_os",
    target_os,
    "--url_mapping=dart:mojo.internal,$dart_mojo_internal_path",
    "--url_mapping=dart:ui,$dart_ui_path",
    "--url_mapping=dart:jni,$dart_jni_path",
  ]
}

action("generate_snapshot_file") {
  deps = [
    ":generate_snapshot_bin",
  ]
  inputs = [
    "//dart/runtime/tools/create_snapshot_file.py",
    "snapshot.c.tmpl",
    "$target_gen_dir/vm_isolate_snapshot.bin",
    "$target_gen_dir/isolate_snapshot.bin",
  ]
  output = "$target_gen_dir/snapshot.c"
  outputs = [
    output,
  ]

  script = "//dart/runtime/tools/create_snapshot_file.py"
  args = [
    "--vm_input_bin",
    rebase_path("$target_gen_dir/vm_isolate_snapshot.bin"),
    "--input_bin",
    rebase_path("$target_gen_dir/isolate_snapshot.bin"),
    "--input_cc",
    rebase_path("snapshot.c.tmpl"),
    "--output",
    rebase_path(output),
  ]
}

source_set("snapshot_cc") {
  sources = [
    "$target_gen_dir/snapshot.c",
  ]

  deps = [
    ":generate_snapshot_file",
  ]
}

copy("generate_dart_ui") {
  sources = [
    "dart_ui.dart",
  ]
  sources += core_dart_files

  outputs = [
    "$bindings_output_dir/{{source_file_part}}",
  ]
}

copy("generate_dart_jni") {
  sources = [
    "jni/jni.dart",
    "jni/jni_helper.dart",
    "jni/jni_raw.dart",
  ]

  outputs = [
    "$bindings_output_dir/dart_jni/{{source_file_part}}",
  ]
}
