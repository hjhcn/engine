# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# TODO(jackson): move this to a separate repo. Doesn't belong in the main
# Flutter repo.

import("//mojo/public/tools/bindings/mojom.gni")

group("firebase") {
  deps = [
    ":interfaces",
  ]

  if (is_ios || is_android) {
    deps += [ ":firebase_lib" ]
  }
}

mojom("interfaces") {
  sources = [
    "firebase.mojom",
  ]
}

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  android_library("firebase_lib") {
    java_files = [
      "src/org/domokit/firebase/FirebaseImpl.java",
    ]

    deps = [
      "//base:base_java",
      "//mojo/java",
      "//mojo/public/java:bindings",
      "//mojo/public/java:system",
      ":interfaces_java",
      "//third_party/firebase",
    ]
  }
}

if (is_ios) {
  source_set("firebase_lib") {
    sources = [
      "firebase.mojom",
      "ios/firebase_impl.h",
      "ios/firebase_impl.mm",
      "//mojo/public/cpp/application/interface_factory.h",
      "//third_party/firebase/Firebase.framework/Headers/Firebase.h",
    ]

    include_dirs = [
      "//third_party/firebase/Firebase.framework/Headers/",
    ]

    libs = [
      "Firebase.framework",
      "CFNetwork.framework",
      "SystemConfiguration.framework",
      "Security.framework",
      "icucore",
    ]

    deps = [
      "//base:base",
      ":interfaces",
    ]

    all_dependent_configs = [ ":firebase_config" ]
  }

  config("firebase_config") {
    firebase_dir = rebase_path("//third_party/firebase/", root_build_dir)
    ldflags = [
      "-F$firebase_dir",
      "-ObjC",
    ]
  }
}
