# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

group("activity") {
  public_deps = [
    ":interfaces",
  ]

  if (is_android || is_ios) {
    public_deps += [ ":activity_lib" ]
  }
}

mojom("interfaces") {
  sources = [
    "activity.mojom",
  ]
}

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  android_library("activity_lib") {
    java_files = [
      "src/org/domokit/activity/ActivityImpl.java",
    ]

    deps = [
      "//base:base_java",
      "//mojo/public/java:bindings",
      "//mojo/public/java:system",
      ":interfaces_java",
    ]
  }
}

if (is_ios) {
  source_set("activity_lib") {
    sources = [
      "ios/activity_impl.h",
      "ios/activity_impl.mm",
    ]
    deps = [
      "//base:base",
      "//mojo/public/cpp/application",
      "//mojo/public/cpp/bindings:bindings",
      "//sky//services/activity:interfaces",
    ]
  }
}
