# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/templates/templates.gni")
import("//sky/engine/bindings/bindings.gni")
import("//sky/build/template.gni")

template("sky_precompilation_sdk") {
  assert(is_ios, "The precompilation SDK is only supported for iOS targets")

  assert(defined(invoker.sdk_name), "The SDK name must be defined")

  sdk_name = invoker.sdk_name
  sdk_dir = "$root_out_dir/$sdk_name"
  tools_dir_name = "Tools"
  tools_dir = "$tools_dir_name/common"

  # The architecture specific suffixes are chosen to match the ${PLATFORM_NAME}
  # used by the .xcconfig files
  arch_tools_dir = "$tools_dir_name/iphoneos"
  if (use_ios_simulator) {
    arch_tools_dir = "$tools_dir_name/iphonesimulator"
  }

  copy("copy_snapshotter") {
    snapshotter_target = "//dart/runtime/bin:gen_snapshot($dart_host_toolchain)"
    snapshotter_directory = get_label_info(snapshotter_target, "root_out_dir")
    snapshotter_name = get_label_info(snapshotter_target, "name")

    sources = [  "$snapshotter_directory/$snapshotter_name"  ]
    outputs = [  "$sdk_dir/$arch_tools_dir/Snapshotter"  ]

    deps = [  snapshotter_target  ]
  }

  action("copy_flutter_framework") {
    stamp_file = "$root_build_dir/copy_flutter_framework.stamp"
    script = "//sky/tools/copy_dir.py"

    inputs = [ "$root_build_dir/Flutter.framework/Flutter" ]
    outputs = [ "$root_build_dir/$sdk_dir/$tools_dir/Flutter.framework/Flutter" ]

    args = [
      "--src",
      rebase_path("$root_build_dir/Flutter.framework"),
      "--dst",
      rebase_path("$sdk_dir/$tools_dir/Flutter.framework"),
      "--stamp",
      rebase_path(stamp_file),
    ]

    deps = [  "//sky/shell:flutter_framework"  ]
  }

  copy("embedder_entry_points") {
    sources = [ "//sky/engine/bindings/dart_vm_entry_points.txt" ]
    outputs = [  "$sdk_dir/$tools_dir/EmbedderEntryPoints"  ]
  }

  copy("precompilation_xcode_scripts") {
    sources = [
      "//sky/build/SnapshotterInvoke",
      "//sky/build/PackagerInvoke",
      "//sky/engine/bindings/snapshot.dart",
    ]
    outputs = [  "$sdk_dir/$tools_dir/{{source_file_part}}"  ]
  }

  copy("copy_sdk_xcode_harness") {
    sources = [
      "//sky/build/sdk_xcode_harness/FlutterApplication",
      "//sky/build/sdk_xcode_harness/FlutterApplication.xcodeproj",
      "//sky/build/sdk_xcode_harness/Local.xcconfig",
      "//sky/build/sdk_xcode_harness/Runner",
    ]

    outputs = [  "$sdk_dir/{{source_file_part}}"  ]
  }

  render_template("flutter_xcconfig") {
    template = "//sky/build/sdk_xcode_harness/Flutter.xcconfig.tmpl"
    output = "$sdk_dir/Flutter.xcconfig"
    stamp_file = "$root_build_dir/expand_flutter_xcconfig.stamp"

    variables = [
      "interpreter"
    ]

    if (dart_experimental_interpreter) {
      variables += [ "1" ]
    } else {
      variables += [ "0" ]
    }
  }

  # All user editable files are copied to the out directory so that developers
  # tinkering on the engine still have a fully functional project harness
  copy("copy_user_editable_files") {
    sources = [
      "//sky/build/Info.plist",
      "//sky/build/LaunchScreen.storyboard",
      "//sky/build/Assets.xcassets",
    ]
    outputs = [  "$root_out_dir/{{source_file_part}}"  ]
  }

  group(target_name) {
    deps = [
      ":copy_flutter_framework",
      ":copy_sdk_xcode_harness",
      ":copy_snapshotter",
      ":copy_user_editable_files",
      ":embedder_entry_points",
      ":flutter_xcconfig",
      ":precompilation_xcode_scripts",
    ]
  }
}
