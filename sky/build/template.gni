# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# A GN template used to process a Jinj2 template file.
template("render_template") {
  set_sources_assignment_filter([])

  assert(defined(invoker.template), "The template file to render.")
  assert(defined(invoker.output), "The output of template expansion.")
  assert(defined(invoker.variables), "The list of template variables.")
  assert(defined(invoker.stamp_file), "The stamp file.")

  action(target_name) {
    if(defined(invoker.visibility)) {
      visibility = invoker.visibility
    }

    script = "//sky/build/template.py"

    sources = [ invoker.template ]
    outputs = [
      invoker.output,
      invoker.stamp_file,
    ]

    args = [
      "--template",
      rebase_path(invoker.template, root_build_dir),
      "--output",
      rebase_path(invoker.output, root_build_dir),
      "--stamp",
      rebase_path(invoker.stamp_file, root_build_dir),
    ]

    args += invoker.variables
  }
}
