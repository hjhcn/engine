# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/templates/templates.gni")
import("//sky/engine/bindings/bindings.gni")
import("//sky/build/template.gni")

template("sky_ios_sdk") {
  assert(is_ios)

  assert(defined(invoker.sdk_name), "The SDK name must be defined")

  sdk_name = invoker.sdk_name
  sdk_dir = "$root_out_dir/$sdk_name"

  copy("copy_sdk_xcode_harness") {
    sources = [
      "//sky/build/sdk_xcode_harness/Flutter",
      "//sky/build/sdk_xcode_harness/FlutterApplication.xcodeproj",
      "//sky/build/sdk_xcode_harness/Runner",
    ]

    outputs = [  "$sdk_dir/{{source_file_part}}"  ]
  }

  # All user editable files are copied to the out directory so that developers
  # tinkering on the engine still have a fully functional project harness
  copy("copy_user_editable_files") {
    sources = [
      "//sky/build/Info.plist",
      "//sky/build/LaunchScreen.storyboard",
      "//sky/build/Assets.xcassets",
    ]
    outputs = [  "$root_out_dir/{{source_file_part}}"  ]
  }

  group(target_name) {
    deps = [
      ":copy_sdk_xcode_harness",
      ":copy_user_editable_files",
    ]
  }
}
