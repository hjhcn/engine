# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("flutter") {
  testonly = true

  deps = [
    "//flutter/lib/snapshot:flutter_patched_sdk",
    "//flutter/lib/snapshot:generate_snapshot_bin",
    "//flutter/sky",
    "//flutter/third_party/txt",
  ]

  if (!is_fuchsia) {
    if (current_toolchain == host_toolchain) {
      deps += [
        "//dart:create_sdk",
        "//flutter/frontend_server",
      ]
    }
  }

  if (is_fuchsia) {
    deps += [
      "//flutter/content_handler",
      "//flutter/content_handler:aot_content_handler",
      "//flutter/examples",
      "//flutter/flow",
    ]
  }

  # If on the host, compile all unittests targets.
  if (current_toolchain == host_toolchain) {
    if (is_mac) {
      deps += [ "//flutter/shell/platform/darwin:flutter_channels_unittests" ]
    }
    if (!is_win) {
      deps += [ "//flutter/shell/platform/embedder:flutter_engine" ]
    }
    deps += [
      "//flutter/flow:flow_unittests",
      "//flutter/fml:fml_unittests",
      "//flutter/sky/engine/wtf:wtf_unittests",
      "//flutter/synchronization:synchronization_unittests",
      "//flutter/third_party/txt:txt_unittests",
      "//garnet/public/lib/ftl:ftl_unittests",
    ]
    if (is_linux) {
      deps += [ "//flutter/third_party/txt:txt_benchmarks" ]
    }
  }
}

if (!is_fuchsia) {
  group("dist") {
    testonly = true

    deps = [
      "//flutter/sky/dist",
    ]
  }
}
